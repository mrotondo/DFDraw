#pragma kernel Clear
#pragma kernel BlitSpheres

RWTexture3D<unorm float> SdfVolumeTexture;

float ClearDistance;
[numthreads(8,8,8)]
void Clear(uint3 id : SV_DispatchThreadID)
{
    SdfVolumeTexture[id.xyz] = ClearDistance;
}

float sphereDistance(float3 samplePoint, float radius)
{
    return length(samplePoint) - radius;
}

struct Sphere
{
    float3 position;
    float radius;
};

int NumSpheres;
int4 TexelOffset;
StructuredBuffer<Sphere> Spheres;
[numthreads(8,8,8)]
void BlitSpheres(uint3 id : SV_DispatchThreadID)
{
    // we assume cubic sdf volume texture when we only use texWidth in distance calculation
    uint texWidth, texHeight, texDepth;
    SdfVolumeTexture.GetDimensions(texWidth, texHeight, texDepth);
    uint3 texelIndex = id.xyz + TexelOffset.xyz;
    float3 samplePoint = texelIndex / (texWidth - 1.0);
    if (NumSpheres > 0)
    {
        float newDistance = 100000;
        for (int i = 0; i < NumSpheres; i++)
        {
            Sphere sphere = Spheres[i];
            newDistance = min(newDistance, sphereDistance(samplePoint - sphere.position, sphere.radius) * 0.5 + 0.5);
        }
        float oldDistance = SdfVolumeTexture[texelIndex];
        if (newDistance < oldDistance)
        {
            SdfVolumeTexture[texelIndex] = newDistance;
        }
    }
}

